
action("build_info_action") {
  gen_dir = rebase_path(target_gen_dir, root_build_dir, ".")
  exec_script("//azer/build/tools/shell/mkdir.py", [gen_dir])
  
  script = "//azer/build/tools/shell/buildinfo.py"
  src_absolute_dir = rebase_path("//")
  outputs = ["$target_gen_dir/build_info.cc"]
  args = [gen_dir, "build_info", src_absolute_dir]
}

static_library("build_info") {
  sources = [
    "$target_gen_dir/build_info.cc",
    "build_info.cc",
    "build_info.h",
  ]
  deps = [":build_info_action"]
}

config("base_config") {
  if (is_win) {
    cflags = ["/wd4819"]
  }
}

config("build_render_config") {
  defines = ["BUILD_AZER_RENDER"]
}

source_set("image_load") {
  sources = ["image_load.cc"]
  deps = ["//base", "//azer/third_party/soil"]
  configs += [":build_render_config"]
}

source_set("base") {
  sources = [
    "align.h",
    "appinit.cc",
    "appinit.h",
    "config_node.cc",
    "config_node.h",
    "dynlib.cc",
    "dynlib.h",
    "file_util.cc",
    "file_util.h",
    "image_data.cc",
    "image_data.h",
    "image.cc",
    "image.h",
    "resource.h",
    "string.cc",
    "string.h",
    "tree_node.h",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//azer/util/xml",
    ":image_load",
  ]

  visibility = ["//azer"]
  public_configs = [":base_config"]
  configs += [":build_render_config"]
}


source_set("unittests") {
  sources = [
    "tree_node_unittest.cc",
  ]

  defines = ["AZER_UNITTEST"]
  deps = [
    "//azer/math",
    "//azer",
  ]
  visibility = ["//azer:unittests"]
}